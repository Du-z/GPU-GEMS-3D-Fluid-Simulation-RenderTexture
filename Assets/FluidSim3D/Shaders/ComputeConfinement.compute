
#pragma kernel CSMain
#define NUM_THREADS 8

float _DeltaTime, _Epsilon;
float4 _Size;

RWTexture3D<float3> _Write;
Texture3D<float3> _Vorticity, _Read;

[numthreads(NUM_THREADS,NUM_THREADS,NUM_THREADS)]
void CSMain (int3 id : SV_DispatchThreadID)
{

    int3 idxL = int3(max(0, id.x-1), id.y, id.z);
    int3 idxR = int3(min(_Size.x-1, id.x+1), id.y, id.z);
    
    int3 idxB = int3(id.x, max(0, id.y-1), id.z);
    int3 idxT = int3(id.x, min(_Size.y-1, id.y+1), id.z);
    
    int3 idxD = int3(id.x, id.y, max(0, id.z-1));
    int3 idxU = int3(id.x, id.y, min(_Size.z-1, id.z+1));
    
	float omegaL = length(_Vorticity[ idxL ]);
    float omegaR = length(_Vorticity[ idxR ]);
    
    float omegaB = length(_Vorticity[ idxB ]);
    float omegaT = length(_Vorticity[ idxT ]);
    
    float omegaD = length(_Vorticity[ idxD ]);
    float omegaU = length(_Vorticity[ idxU ]);
    
    float3 omega = _Vorticity[id];
    
    float3 eta = 0.5 * float3( omegaR - omegaL, omegaT - omegaB, omegaU - omegaD );

    eta = normalize( eta + float3(0.001,0.001,0.001) );
    
    float3 force = _DeltaTime * _Epsilon * float3( eta.y * omega.z - eta.z * omega.y, eta.z * omega.x - eta.x * omega.z, eta.x * omega.y - eta.y * omega.x );
	
    _Write[id] = _Read[id] + force;
}
